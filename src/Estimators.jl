"""
	NeuralEstimator

An abstract supertype for all neural estimators in `NeuralEstimators.jl`.
"""
abstract type NeuralEstimator end

"""
	BayesEstimator <: NeuralEstimator

An abstract supertype for neural Bayes estimators.
"""
abstract type BayesEstimator <: NeuralEstimator  end

"""
	PointEstimator <: BayesEstimator
    PointEstimator(network)
	(estimator::PointEstimator)(Z)
A point estimator, where the neural `network` is a mapping from the sample space to the parameter space.
"""
struct PointEstimator <: BayesEstimator
	network 
end
(estimator::PointEstimator)(Z) = estimator.network(Z)

#TODO Single shared summary statistic computation for efficiency
#TODO enforce probs âˆˆ (0, 1)
@doc raw"""
	IntervalEstimator <: BayesEstimator
	IntervalEstimator(u, v = u; probs = [0.025, 0.975], g::Function = exp)
	IntervalEstimator(u, c::Union{Function, Compress}; probs = [0.025, 0.975], g::Function = exp)
	IntervalEstimator(u, v, c::Union{Function, Compress}; probs = [0.025, 0.975], g::Function = exp)
	(estimator::IntervalEstimator)(Z)
A neural estimator that jointly estimates marginal posterior credible intervals based on the probability levels `probs` (by default, 95% central credible intervals).

The estimator employs a representation that prevents quantile crossing. Specifically, given data ``\boldsymbol{Z}``, 
it constructs intervals for each parameter
``\theta_i``, ``i = 1, \dots, d,``  of the form,
```math
[c_i(u_i(\boldsymbol{Z})), \;\; c_i(u_i(\boldsymbol{Z})) + g(v_i(\boldsymbol{Z})))],
```
where  ``\boldsymbol{u}(â‹…) \equiv (u_1(\cdot), \dots, u_d(\cdot))'`` and
``\boldsymbol{v}(â‹…) \equiv (v_1(\cdot), \dots, v_d(\cdot))'`` are neural networks
that map from the sample space to ``\mathbb{R}^d``; $g(\cdot)$ is a
monotonically increasing function (e.g., exponential or softplus); and each
``c_i(â‹…)`` is a monotonically increasing function that maps its input to the
prior support of ``\theta_i``.

The functions ``c_i(â‹…)`` may be collectively defined by a ``d``-dimensional object of type
[`Compress`](@ref). If these functions are unspecified, they will be set to the
identity function so that the range of the intervals will be unrestricted. 
If only a single neural-network architecture is provided, it will be used for both ``\boldsymbol{u}(â‹…)`` and ``\boldsymbol{v}(â‹…)``.

The return value when applied to data using [`estimate`()](@ref) is a matrix with ``2d`` rows, where the first and second ``d`` rows correspond to the lower and upper bounds, respectively. The function [`interval()`](@ref) can be used to format this output in a readable ``d`` Ã— 2 matrix.  

See also [`QuantileEstimatorDiscrete`](@ref) and
[`QuantileEstimatorContinuous`](@ref).

# Examples
```
using NeuralEstimators, Flux

# Data Z|Î¼,Ïƒ ~ N(Î¼, ÏƒÂ²) with priors Î¼ ~ U(0, 1) and Ïƒ ~ U(0, 1)
d = 2     # dimension of the parameter vector Î¸
n = 1     # dimension of each independent replicate of Z
m = 100   # number of independent replicates
sample(K) = rand32(d, K)
simulate(Î¸, m) = [Ï‘[1] .+ Ï‘[2] .* randn(n, m) for Ï‘ in eachcol(Î¸)]

# Neural network
w = 128   # width of each hidden layer
Ïˆ = Chain(Dense(n, w, relu), Dense(w, w, relu))
Ï• = Chain(Dense(w, w, relu), Dense(w, d))
u = DeepSet(Ïˆ, Ï•)

# Initialise the estimator
estimator = IntervalEstimator(u)

# Train the estimator
estimator = train(estimator, sample, simulate, m = m)

# Inference with "observed" data 
Î¸ = [0.8f0; 0.1f0]
Z = simulate(Î¸, m)
estimate(estimator, Z) 
interval(estimator, Z)
```
"""
struct IntervalEstimator{H} <: BayesEstimator
	u::DeepSet
	v::DeepSet
	c::Union{Function,Compress}
	probs::H
	g::Function
end
IntervalEstimator(u::DeepSet, v::DeepSet = u; probs = [0.025, 0.975], g = exp) = IntervalEstimator(deepcopy(u), deepcopy(v), identity, probs, g)
IntervalEstimator(u::DeepSet, c::Compress; probs = [0.025, 0.975], g = exp) = IntervalEstimator(deepcopy(u), deepcopy(u), c, probs, g)
IntervalEstimator(u::DeepSet, v::DeepSet, c::Compress; probs = [0.025, 0.975], g = exp) = IntervalEstimator(deepcopy(u), deepcopy(v), c, probs, g)
Flux.trainable(est::IntervalEstimator) = (u = est.u, v = est.v)
function (est::IntervalEstimator)(Z)
	bâ‚— = est.u(Z)                # lower bound
	báµ¤ = bâ‚— .+ est.g.(est.v(Z))  # upper bound
	vcat(est.c(bâ‚—), est.c(báµ¤))
end

#TODO Single shared summary statistic computation for efficiency
#TODO improve print output
#TODO function for neat output as dxT matrix like interval() 
@doc raw"""
	QuantileEstimatorDiscrete <: BayesEstimator
	QuantileEstimatorDiscrete(v; probs = [0.05, 0.25, 0.5, 0.75, 0.95], g = Flux.softplus, i = nothing)
	(estimator::QuantileEstimatorDiscrete)(Z)
	(estimator::QuantileEstimatorDiscrete)(Z, Î¸â‚‹áµ¢)

A neural estimator that jointly estimates a fixed set of marginal posterior
quantiles, with probability levels $\{\tau_1, \dots, \tau_T\}$ controlled by the
keyword argument `probs`. This generalises [`IntervalEstimator`](@ref) to support an arbitrary number of probability levels. 

Given data ``\boldsymbol{Z}``, by default the estimator approximates quantiles of the distributions of 
```math
\theta_i \mid \boldsymbol{Z}, \quad i = 1, \dots, d, 
```
for parameters $\boldsymbol{\theta} \equiv (\theta_1, \dots, \theta_d)'$.
Alternatively, if initialised with `i` set to a positive integer, the estimator approximates quantiles of
the full conditional distribution of  
```math
\theta_i \mid \boldsymbol{Z}, \boldsymbol{\theta}_{-i},
```
where $\boldsymbol{\theta}_{-i}$ denotes the parameter vector with its $i$th
element removed. 

The estimator employs a representation that prevents quantile crossing, namely,
```math
\begin{aligned}
\boldsymbol{q}^{(\tau_1)}(\boldsymbol{Z}) &= \boldsymbol{v}^{(\tau_1)}(\boldsymbol{Z}),\\
\boldsymbol{q}^{(\tau_t)}(\boldsymbol{Z}) &= \boldsymbol{v}^{(\tau_1)}(\boldsymbol{Z}) + \sum_{j=2}^t g(\boldsymbol{v}^{(\tau_j)}(\boldsymbol{Z})), \quad t = 2, \dots, T,
\end{aligned}
```
where $\boldsymbol{q}^{(\tau)}(\boldsymbol{Z})$ denotes the vector of $\tau$-quantiles 
for parameters $\boldsymbol{\theta} \equiv (\theta_1, \dots, \theta_d)'$; 
$\boldsymbol{v}^{(\tau_t)}(\cdot)$, $t = 1, \dots, T$, are neural networks
that map from the sample space to ``\mathbb{R}^d``; and $g(\cdot)$ is a
monotonically increasing function (e.g., exponential or softplus) applied elementwise to
its arguments. If `g = nothing`, the quantiles are estimated independently through the representation
```math
\boldsymbol{q}^{(\tau_t)}(\boldsymbol{Z}) = \boldsymbol{v}^{(\tau_t)}(\boldsymbol{Z}), \quad t = 1, \dots, T.
```

When the neural networks are [`DeepSet`](@ref) objects, two requirements must be met. 
First, the number of input neurons in the first layer of the outer network must equal the number of
neurons in the final layer of the inner network plus $\text{dim}(\boldsymbol{\theta}_{-i})$, where we define 
$\text{dim}(\boldsymbol{\theta}_{-i}) \equiv 0$ when targetting marginal posteriors of the form $\theta_i \mid \boldsymbol{Z}$ (the default behaviour). 
Second, the number of output neurons in the final layer of the outer network must equal $d - \text{dim}(\boldsymbol{\theta}_{-i})$. 

The return value is a matrix with $\{d - \text{dim}(\boldsymbol{\theta}_{-i})\} \times T$ rows, where the
first ``T`` rows correspond to the estimated quantiles for the first
parameter, the second ``T`` rows corresponds to the estimated quantiles for the second parameter, and so on.

See also [`QuantileEstimatorContinuous`](@ref).

# Examples
```
using NeuralEstimators, Flux

# Data Z|Î¼,Ïƒ ~ N(Î¼, ÏƒÂ²) with priors Î¼ ~ U(0, 1) and Ïƒ ~ U(0, 1)
d = 2     # dimension of the parameter vector Î¸
n = 1     # dimension of each independent replicate of Z
m = 30    # number of independent replicates in each data set
sample(K) = rand32(d, K)
simulate(Î¸, m) = [Ï‘[1] .+ Ï‘[2] .* randn32(n, m) for Ï‘ in eachcol(Î¸)]

# ---- Quantiles of Î¸áµ¢ âˆ£ ğ™, i = 1, â€¦, d ----

# Neural network
w = 64   # width of each hidden layer
Ïˆ = Chain(Dense(n, w, relu), Dense(w, w, relu))
Ï• = Chain(Dense(w, w, relu), Dense(w, d))
v = DeepSet(Ïˆ, Ï•)

# Initialise the estimator
Ï„ = [0.05, 0.25, 0.5, 0.75, 0.95]
estimator = QuantileEstimatorDiscrete(v; probs = Ï„)

# Train the estimator
estimator = train(estimator, sample, simulate, m = m)

# Inference with "observed" data 
Î¸ = [0.8f0; 0.1f0]
Z = simulate(Î¸, m)
estimate(estimator, Z) 

# ---- Quantiles of Î¸áµ¢ âˆ£ ğ™, Î¸â‚‹áµ¢ ----

# Neural network
w = 64  # width of each hidden layer
Ïˆ = Chain(Dense(n, w, relu), Dense(w, w, relu))
Ï• = Chain(Dense(w + 1, w, relu), Dense(w, d - 1))
v = DeepSet(Ïˆ, Ï•)

# Initialise estimators respectively targetting quantiles of Î¼âˆ£Z,Ïƒ and Ïƒâˆ£Z,Î¼
Ï„ = [0.05, 0.25, 0.5, 0.75, 0.95]
qâ‚ = QuantileEstimatorDiscrete(v; probs = Ï„, i = 1)
qâ‚‚ = QuantileEstimatorDiscrete(v; probs = Ï„, i = 2)

# Train the estimators
qâ‚ = train(qâ‚, sample, simulate, m = m)
qâ‚‚ = train(qâ‚‚, sample, simulate, m = m)

# Estimate quantiles of Î¼âˆ£Z,Ïƒ with Ïƒ = 0.5 and for many data sets
Î¸â‚‹áµ¢ = 0.5f0
qâ‚(Z, Î¸â‚‹áµ¢)

# Estimate quantiles of Î¼âˆ£Z,Ïƒ with Ïƒ = 0.5 for a single data set
qâ‚(Z[1], Î¸â‚‹áµ¢)
```
"""
struct QuantileEstimatorDiscrete{V, P} <: BayesEstimator
	v::V
	probs::P
	g::Union{Function, Nothing}
	i::Union{Integer, Nothing}
end
function QuantileEstimatorDiscrete(v; probs = [0.05, 0.25, 0.5, 0.75, 0.95], g = Flux.softplus, i::Union{Integer, Nothing} = nothing)
	if !isnothing(i) @assert i > 0 end
	QuantileEstimatorDiscrete(deepcopy.(repeat([v], length(probs))), probs, g, i)
end
Flux.trainable(est::QuantileEstimatorDiscrete) = (v = est.v, )
function (est::QuantileEstimatorDiscrete)(input) # input might be Z, or a tuple (Z, Î¸â‚‹áµ¢)

	# Apply each neural network to Z
	v = map(est.v) do v
		v(input)
	end

	# If g is specified, impose monotonicity
	if isnothing(est.g)
		q = v
	else
		gv = broadcast.(est.g, v[2:end])
		q = cumsum([v[1], gv...])
	end

	# Convert to matrix
	reduce(vcat, q)
end
# user-level convenience methods (not used internally) for full conditional estimation
function (est::QuantileEstimatorDiscrete)(Z, Î¸â‚‹áµ¢::Vector)
	i = est.i
	@assert !isnothing(i) "slot i must be specified when approximating a full conditional"
	if isa(Z, Vector) # repeat Î¸â‚‹áµ¢ to match the number of data sets
		Î¸â‚‹áµ¢ = [Î¸â‚‹áµ¢ for _ in eachindex(Z)]
	end
	est((Z, Î¸â‚‹áµ¢))  # "Tupleise" the input and apply the estimator
end
(est::QuantileEstimatorDiscrete)(Z, Î¸â‚‹áµ¢::Number) = est(Z, [Î¸â‚‹áµ¢])

# Assess the estimators
# using AlgebraOfGraphics, CairoMakie
# Î¸ = sample(1000)
# Z = simulate(Î¸, m)
# assessment = assess([qâ‚, qâ‚‚], Î¸, Z, parameter_names = ["Î¼", "Ïƒ"])
# plot(assessment)

# function posterior(Z; Î¼â‚€ = 0, Ïƒâ‚€ = 1, ÏƒÂ² = 1)
# 	Î¼Ìƒ = (1/Ïƒâ‚€^2 + length(Z)/ÏƒÂ²)^-1 * (Î¼â‚€/Ïƒâ‚€^2 + sum(Z)/ÏƒÂ²)
# 	ÏƒÌƒ = sqrt((1/Ïƒâ‚€^2 + length(Z)/ÏƒÂ²)^-1)
# 	Normal(Î¼Ìƒ, ÏƒÌƒ)
# end

#TODO incorporate this into docs somewhere: It's based on the fact that a pair (Î¸áµ, Záµ) sampled as Î¸áµ âˆ¼ p(Î¸), Záµ ~ p(Z âˆ£ Î¸áµ) is also a sample from Î¸áµ âˆ¼ p(Î¸ âˆ£ Záµ), Záµ ~ p(Z).
#TODO clarify output structure when we have multiple probability levels (what is the ordering in this case?)
@doc raw"""
	QuantileEstimatorContinuous <: BayesEstimator
	QuantileEstimatorContinuous(network; i = nothing, num_training_probs::Integer = 1)
	(estimator::QuantileEstimatorContinuous)(Z, Ï„)
	(estimator::QuantileEstimatorContinuous)(Z, Î¸â‚‹áµ¢, Ï„)

A neural estimator that estimates marginal posterior quantiles, with the probability level `Ï„` given as input to the neural network.

Given data $\boldsymbol{Z}$ and the desired probability level 
$\tau âˆˆ (0, 1)$, by default the estimator approximates the $\tau$-quantile of the distributions of 
```math
\theta_i \mid \boldsymbol{Z}, \quad i = 1, \dots, d, 
```
for parameters $\boldsymbol{\theta} \equiv (\theta_1, \dots, \theta_d)'$.
Alternatively, if initialised with `i` set to a positive integer, the estimator
approximates the $\tau$-quantile of the full conditional distribution of 
```math
\theta_i \mid \boldsymbol{Z}, \boldsymbol{\theta}_{-i},
```
where $\boldsymbol{\theta}_{-i}$ denotes the parameter vector with its $i$th element removed. 

Although not a requirement, one may employ a (partially) monotonic neural
network to prevent quantile crossing (i.e., to ensure that the
$\tau_1$-quantile does not exceed the $\tau_2$-quantile for any
$\tau_2 > \tau_1$). There are several ways to construct such a neural network:
one simple yet effective approach is to ensure that all weights associated with
$\tau$ are strictly positive
(see, e.g., [Cannon, 2018](https://link.springer.com/article/10.1007/s00477-018-1573-6)),
and this can be done using the [`DensePositive`](@ref) layer as shown in the example below.

When the neural network is a [`DeepSet`](@ref), two requirements must be met. First, the number of input neurons in the first layer of the outer network must equal the number of
neurons in the final layer of the inner network plus $1 + \text{dim}(\boldsymbol{\theta}_{-i})$, where we define 
$\text{dim}(\boldsymbol{\theta}_{-i}) \equiv 0$ when targetting marginal posteriors of the form $\theta_i \mid \boldsymbol{Z}$ (the default behaviour). 
Second, the number of output neurons in the final layer of the outer network must equal $d - \text{dim}(\boldsymbol{\theta}_{-i})$. 

The return value is a matrix with $d - \text{dim}(\boldsymbol{\theta}_{-i})$ rows,
corresponding to the estimated quantile for each parameter not in $\boldsymbol{\theta}_{-i}$.

See also [`QuantileEstimatorDiscrete`](@ref).

# Examples
```
using NeuralEstimators, Flux

# Data Z|Î¼,Ïƒ ~ N(Î¼, ÏƒÂ²) with priors Î¼ ~ U(0, 1) and Ïƒ ~ U(0, 1)
d = 2     # dimension of the parameter vector Î¸
n = 1     # dimension of each independent replicate of Z
m = 30    # number of independent replicates in each data set
sample(K) = rand32(d, K)
simulateZ(Î¸, m) = [Ï‘[1] .+ Ï‘[2] .* randn32(n, m) for Ï‘ in eachcol(Î¸)]
simulateÏ„(K)    = [rand32(10) for k in 1:K]
simulate(Î¸, m)  = simulateZ(Î¸, m), simulateÏ„(size(Î¸, 2))

# ---- Quantiles of Î¸áµ¢ âˆ£ ğ™, i = 1, â€¦, d ----

# Neural network: partially monotonic network to preclude quantile crossing
w = 64  # width of each hidden layer
Ïˆ = Chain(
	Dense(n, w, relu),
	Dense(w, w, relu),
	Dense(w, w, relu)
	)
Ï• = Chain(
	DensePositive(Dense(w + 1, w, relu); last_only = true),
	DensePositive(Dense(w, w, relu)),
	DensePositive(Dense(w, d))
	)
network = DeepSet(Ïˆ, Ï•)

# Initialise the estimator
qÌ‚ = QuantileEstimatorContinuous(network)

# Train the estimator
qÌ‚ = train(qÌ‚, sample, simulate, m = m)

# Test data 
Î¸ = sample(1000)
Z = simulateZ(Î¸, m)

# Estimate 0.1-quantile for each parameter and for many data sets
Ï„ = 0.1f0
qÌ‚(Z, Ï„)

# Estimate multiple quantiles for each parameter and for many data sets
# (note that Ï„ is given as a row vector)
Ï„ = f32([0.1, 0.25, 0.5, 0.75, 0.9])'
qÌ‚(Z, Ï„)

# Estimate multiple quantiles for a single data set 
qÌ‚(Z[1], Ï„)

# ---- Quantiles of Î¸áµ¢ âˆ£ ğ™, Î¸â‚‹áµ¢ ----

# Neural network: partially monotonic network to preclude quantile crossing
w = 64  # width of each hidden layer
Ïˆ = Chain(
	Dense(n, w, relu),
	Dense(w, w, relu),
	Dense(w, w, relu)
	)
Ï• = Chain(
	DensePositive(Dense(w + 2, w, relu); last_only = true),
	DensePositive(Dense(w, w, relu)),
	DensePositive(Dense(w, d - 1))
	)
network = DeepSet(Ïˆ, Ï•)

# Initialise the estimator targetting Î¼âˆ£Z,Ïƒ
i = 1
qÌ‚áµ¢ = QuantileEstimatorContinuous(network; i = i)

# Train the estimator
qÌ‚áµ¢ = train(qÌ‚áµ¢, prior, simulate, m = m)

# Test data 
Î¸ = sample(1000)
Z = simulateZ(Î¸, m)

# Estimate quantiles of Î¼âˆ£Z,Ïƒ with Ïƒ = 0.5 and for many data sets
# (can use Î¸[InvertedIndices.Not(i), :] to determine the order in which the conditioned parameters should be given)
Î¸â‚‹áµ¢ = 0.5f0
Ï„ = f32([0.1, 0.25, 0.5, 0.75, 0.9])
qÌ‚áµ¢(Z, Î¸â‚‹áµ¢, Ï„)

# Estimate quantiles of Î¼âˆ£Z,Ïƒ with Ïƒ = 0.5 and for a single data set
qÌ‚áµ¢(Z[1], Î¸â‚‹áµ¢, Ï„)
```
"""
struct QuantileEstimatorContinuous <: NeuralEstimator
	deepset::DeepSet #TODO remove ::DeepSet
	i::Union{Integer, Nothing}
end
function QuantileEstimatorContinuous(deepset::DeepSet; i::Union{Integer, Nothing} = nothing)
	if !isnothing(i) @assert i > 0 end
	QuantileEstimatorContinuous(deepset, i)
end
# core method (used internally)
(est::QuantileEstimatorContinuous)(tup::Tuple) = est.deepset(tup)
# user-level convenience functions (not used internally)
function (est::QuantileEstimatorContinuous)(Z, Ï„)
	if !isnothing(est.i)
		error("To estimate the Ï„-quantile of the full conditional Î¸áµ¢|Z,Î¸â‚‹áµ¢ the call should be of the form estimator(Z, Î¸â‚‹áµ¢, Ï„)")
	end
	est((Z, Ï„)) # "Tupleise" input and pass to Tuple method
end
function (est::QuantileEstimatorContinuous)(Z, Ï„::Number)
	est(Z, [Ï„])
end
function (est::QuantileEstimatorContinuous)(Z::V, Ï„::Number) where V <: AbstractVector{A} where A
	est(Z, repeat([[Ï„]],  length(Z)))
end
# user-level convenience functions (not used internally) for full conditional estimation
function (est::QuantileEstimatorContinuous)(Z, Î¸â‚‹áµ¢::Matrix, Ï„::Matrix)
	i = est.i
	@assert !isnothing(i) "slot i must be specified when approximating a full conditional"
	if size(Î¸â‚‹áµ¢, 2) != size(Ï„, 2)
		@assert size(Î¸â‚‹áµ¢, 2) == 1 "size(Î¸â‚‹áµ¢, 2)=$(size(Î¸â‚‹áµ¢, 2)) and size(Ï„, 2)=$(size(Ï„, 2)) do not match"
		Î¸â‚‹áµ¢ = repeat(Î¸â‚‹áµ¢, outer = (1, size(Ï„, 2)))
	end
	Î¸â‚‹áµ¢Ï„ = vcat(Î¸â‚‹áµ¢, Ï„) # combine parameters and probability level into single pxK matrix
	q = est((Z, Î¸â‚‹áµ¢Ï„))  # "Tupleise" the input and pass to tuple method
	if !isa(q, Vector) q = [q] end
	reduce(hcat, permutedims.(q))
end
(est::QuantileEstimatorContinuous)(Z, Î¸â‚‹áµ¢::Matrix, Ï„::Vector) = est(Z, Î¸â‚‹áµ¢, permutedims(reduce(vcat, Ï„)))
(est::QuantileEstimatorContinuous)(Z, Î¸â‚‹áµ¢::Matrix, Ï„::Number) = est(Z, Î¸â‚‹áµ¢, repeat([Ï„], size(Î¸â‚‹áµ¢, 2)))
(est::QuantileEstimatorContinuous)(Z, Î¸â‚‹áµ¢::Vector, Ï„::Vector) = est(Z, reshape(Î¸â‚‹áµ¢, :, 1), permutedims(Ï„))
(est::QuantileEstimatorContinuous)(Z, Î¸â‚‹áµ¢::Vector, Ï„::Number) = est(Z, Î¸â‚‹áµ¢, [Ï„])
(est::QuantileEstimatorContinuous)(Z, Î¸â‚‹áµ¢::Number, Ï„::Number) = est(Z, [Î¸â‚‹áµ¢], Ï„)
(est::QuantileEstimatorContinuous)(Z, Î¸â‚‹áµ¢::Number, Ï„::Vector) = est(Z, [Î¸â‚‹áµ¢], Ï„)

# # Closed-form posterior for comparison
# function posterior(Z; Î¼â‚€ = 0, Ïƒâ‚€ = 1, ÏƒÂ² = 1)

# 	# Parameters of posterior distribution
# 	Î¼Ìƒ = (1/Ïƒâ‚€^2 + length(Z)/ÏƒÂ²)^-1 * (Î¼â‚€/Ïƒâ‚€^2 + sum(Z)/ÏƒÂ²)
# 	ÏƒÌƒ = sqrt((1/Ïƒâ‚€^2 + length(Z)/ÏƒÂ²)^-1)

# 	# Posterior
# 	Normal(Î¼Ìƒ, ÏƒÌƒ)
# end

# # Estimate the posterior 0.1-quantile for 1000 test data sets
# Ï„ = 0.1f0
# qÌ‚(Z, Ï„)                        # neural quantiles
# quantile.(posterior.(Z), Ï„)'   # true quantiles

# # Estimate several quantiles for a single data set
# z = Z[1]
# Ï„ = f32([0.1, 0.25, 0.5, 0.75, 0.9])
# qÌ‚(z, Ï„')                     # neural quantiles (note that Ï„ is given as row vector)
# quantile.(posterior(z), Ï„)   # true quantiles

@doc raw"""
	PosteriorEstimator <: NeuralEstimator
	PosteriorEstimator(q::ApproximateDistribution, network)
	sampleposterior(estimator::PosteriorEstimator, Z, N::Integer)
	posteriormean(estimator::PosteriorEstimator)
A neural estimator that approximates the posterior distribution $p(\boldsymbol{\theta} \mid \boldsymbol{Z})$. 

The neural `network` is a mapping from the sample space to a space that depends on the chosen approximate distribution `q` (see the available in-built [Approximate distributions](@ref)). 
Often, the output space of the neural network is the space $\mathcal{K}$ of approximate-distribution parameters $\boldsymbol{\kappa}$.  
However, for certain approximate distributions (notably, [`NormalisingFlow`](@ref)), the neural network should output summary statistics of some suitable dimension (e.g., the dimension $d$ of the parameter vector). 

# Examples
```
using NeuralEstimators, Flux

# Data Z|Î¼,Ïƒ ~ N(Î¼, ÏƒÂ²) with priors Î¼ ~ U(0, 1) and Ïƒ ~ U(0, 1)
d = 2     # dimension of the parameter vector Î¸
n = 1     # dimension of each independent replicate of Z
m = 30    # number of independent replicates in each data set
sample(K) = rand32(d, K)
simulate(Î¸, m) = [Ï‘[1] .+ Ï‘[2] .* randn32(n, m) for Ï‘ in eachcol(Î¸)]

# Distribution used to approximate the posterior 
q = NormalisingFlow(d, d) 

# Neural network (outputs d summary statistics)
w = 128   
Ïˆ = Chain(Dense(n, w, relu), Dense(w, w, relu), Dense(w, w, relu))
Ï• = Chain(Dense(w, w, relu), Dense(w, w, relu), Dense(w, d))
network = DeepSet(Ïˆ, Ï•)

## Alternatively, to use a Gaussian approximate distribution: 
# q = GaussianDistribution(d) 
# w = 128
# Ïˆ = Chain(Dense(n, w, relu), Dense(w, w, relu), Dense(w, w, relu))
# Ï• = Chain(Dense(w, w, relu), Dense(w, w, relu), Dense(w, numdistributionalparams(q)))
# network = DeepSet(Ïˆ, Ï•)

# Initialise the estimator
estimator = PosteriorEstimator(q, network)

# Train the estimator
estimator = train(estimator, sample, simulate, m = m)

# Inference with observed data 
Î¸ = [0.8f0; 0.1f0]
Z = simulate(Î¸, m)
sampleposterior(estimator, Z) # posterior draws 
posteriormean(estimator, Z)   # point estimate
```
"""
struct PosteriorEstimator <: NeuralEstimator
	q::ApproximateDistribution
	network
end
numdistributionalparams(estimator::PosteriorEstimator) = numdistributionalparams(estimator.q)
logdensity(estimator::PosteriorEstimator, Î¸, Z) = logdensity(estimator.q, Î¸, estimator.network(Z)) 
(estimator::PosteriorEstimator)(ZÎ¸::Tuple) = logdensity(estimator, ZÎ¸[2], ZÎ¸[1]) # internal method only used for convenience during training # TODO not ideal that we assume an ordering here
sampleposterior(estimator::PosteriorEstimator, Z, N::Integer = 1000) = sampleposterior(estimator.q, estimator.network(Z), N)

# <!-- There are also practical advantages to considering the likelihood-to-evidence ratio: for example, given conditionally (on $\boldsymbol{\theta}$) independent and identically distributed (iid) replicates $\boldsymbol{Z}_1, \dots, \boldsymbol{Z}_m$, the likelihood-to-evidence ratio is of the form $p(\boldsymbol{Z}_1, \dots, \boldsymbol{Z}_m \mid \boldsymbol{\theta}) / p(\boldsymbol{Z}_1, \dots, \boldsymbol{Z}_m) \propto \prod_{i=1}^m r(\boldsymbol{Z}_i, \boldsymbol{\theta})$, that is, a product of single-replicate likelihood-to-evidence ratios.  -->
@doc raw"""
	RatioEstimator <: NeuralEstimator
	RatioEstimator(network)
	(estimator::RatioEstimator)(Z, Î¸)
	sampleposterior(estimator::RatioEstimator, Z, N::Integer)
A neural estimator that estimates the likelihood-to-evidence ratio,
```math
r(\boldsymbol{Z}, \boldsymbol{\theta}) \equiv p(\boldsymbol{Z} \mid \boldsymbol{\theta})/p(\boldsymbol{Z}),
```
where $p(\boldsymbol{Z} \mid \boldsymbol{\theta})$ is the likelihood and $p(\boldsymbol{Z})$
is the marginal likelihood, also known as the model evidence.

For numerical stability, training is done on the log-scale using the relation 
$\log r(\boldsymbol{Z}, \boldsymbol{\theta}) = \text{logit}(c^*(\boldsymbol{Z}, \boldsymbol{\theta}))$, 
where $c^*(\cdot, \cdot)$ denotes the Bayes classifier as described in the [Methodology](@ref) section. 
Hence, the neural `network` should be a mapping from $\mathcal{Z} \times \Theta$ to $\mathbb{R}$, where $\mathcal{Z}$ and $\Theta$ denote the sample and parameter spaces, respectively. 

When the neural network is a [`DeepSet`](@ref), two requirements must be met. First, the number of input neurons in the first layer of
the outer network must equal $d$ plus the number of output neurons in the final layer of the inner network. 
Second, the number of output neurons in the final layer of the outer network must be one.

When applying the estimator to data `Z`, by default the likelihood-to-evidence ratio
$r(\boldsymbol{Z}, \boldsymbol{\theta})$ is returned (setting the keyword argument
`classifier = true` will yield class probability estimates). The estimated ratio
can then be used in various Bayesian
(e.g., [Hermans et al., 2020](https://proceedings.mlr.press/v119/hermans20a.html))
or frequentist
(e.g., [Walchessen et al., 2024](https://doi.org/10.1016/j.spasta.2024.100848))
inferential algorithms.

See also [`mlestimate`](@ref) and [`mapestimate`](@ref) for obtaining
approximate maximum-likelihood and maximum-a-posteriori estimates, and
[`sampleposterior`](@ref) for obtaining approximate posterior samples.

# Examples
```
using NeuralEstimators, Flux

# Data Z|Î¼,Ïƒ ~ N(Î¼, ÏƒÂ²) with priors Î¼ ~ U(0, 1) and Ïƒ ~ U(0, 1)
d = 2     # dimension of the parameter vector Î¸
n = 1     # dimension of each independent replicate of Z
m = 30    # number of independent replicates in each data set
sample(K) = rand32(d, K)
simulate(Î¸, m) = [Ï‘[1] .+ Ï‘[2] .* randn32(n, m) for Ï‘ in eachcol(Î¸)]

# Neural network
w = 128 
Ïˆ = Chain(Dense(n, w, relu), Dense(w, w, relu), Dense(w, w, relu))
Ï• = Chain(Dense(w + d, w, relu), Dense(w, w, relu), Dense(w, 1))
network = DeepSet(Ïˆ, Ï•)

# Initialise the estimator
rÌ‚ = RatioEstimator(network)

# Train the estimator
rÌ‚ = train(rÌ‚, sample, simulate, m = m)

# Inference with "observed" data (grid-based optimisation and sampling)
Î¸ = sample(1)
z = simulate(Î¸, m)[1]
Î¸_grid = f32(expandgrid(0:0.01:1, 0:0.01:1))'  # fine gridding of the parameter space
rÌ‚(z, Î¸_grid)                                   # likelihood-to-evidence ratios over grid
mlestimate(rÌ‚, z; Î¸_grid = Î¸_grid)              # maximum-likelihood estimate
mapestimate(rÌ‚, z; Î¸_grid = Î¸_grid)             # maximum-a-posteriori estimate
sampleposterior(rÌ‚, z; Î¸_grid = Î¸_grid)         # posterior samples

# Inference with "observed" data (gradient-based optimisation using Optim.jl)
using Optim
Î¸â‚€ = [0.5, 0.5]                                # initial estimate
mlestimate(rÌ‚, z; Î¸â‚€ = Î¸â‚€)                      # maximum-likelihood estimate
mapestimate(rÌ‚, z; Î¸â‚€ = Î¸â‚€)                     # maximum-a-posteriori estimate
```
"""
struct RatioEstimator <: NeuralEstimator
	deepset::DeepSet #TODO remove ::DeepSet
end
function (estimator::RatioEstimator)(Z, Î¸; kwargs...)
	estimator((Z, Î¸); kwargs...) # "Tupleise" the input and pass to Tuple method
end
function (estimator::RatioEstimator)(ZÎ¸::Tuple; classifier::Bool = false)
	c = Ïƒ(estimator.deepset(ZÎ¸))
	if typeof(c) <: AbstractVector
		c = reduce(vcat, c)
	end
	classifier ? c : c ./ (1 .- c)
end

# # Estimate ratio for many data sets and parameter vectors
# Î¸ = sample(1000)
# Z = simulate(Î¸, m)
# rÌ‚(Z, Î¸)                                   # likelihood-to-evidence ratios
# rÌ‚(Z, Î¸; classifier = true)                # class probabilities

# # Inference with multiple data sets
# Î¸ = sample(10)
# z = simulate(Î¸, m)
# rÌ‚(z, Î¸_grid)                                       # likelihood-to-evidence ratios
# mlestimate(rÌ‚, z; Î¸_grid = Î¸_grid)                  # maximum-likelihood estimates
# mlestimate(rÌ‚, z; Î¸â‚€ = Î¸â‚€)                          # maximum-likelihood estimates
# samples = sampleposterior(rÌ‚, z; Î¸_grid = Î¸_grid)   # posterior samples
# Î¸Ì„ = reduce(hcat, mean.(samples; dims = 2))         # posterior means
# interval.(samples; probs = [0.05, 0.95])           # posterior credible intervals

@doc raw"""
	PiecewiseEstimator(estimators, changepoints)
Creates a piecewise estimator
([Sainsbury-Dale et al., 2024](https://www.tandfonline.com/doi/full/10.1080/00031305.2023.2249522), sec. 2.2.2)
from a collection of `estimators` and sample-size `changepoints`.

Specifically, with $l$ estimators and sample-size changepoints
$m_1 < m_2 < \dots < m_{l-1}$, the piecewise etimator takes the form,

```math
\hat{\boldsymbol{\theta}}(\boldsymbol{Z})
=
\begin{cases}
\hat{\boldsymbol{\theta}}_1(\boldsymbol{Z}) & m \leq m_1,\\
\hat{\boldsymbol{\theta}}_2(\boldsymbol{Z}) & m_1 < m \leq m_2,\\
\quad \vdots \\
\hat{\boldsymbol{\theta}}_l(\boldsymbol{Z}) & m > m_{l-1}.
\end{cases}
```

For example, given an estimator ``\hat{\boldsymbol{\theta}}_1(\cdot)`` trained for small
sample sizes (e.g., ``m \leq 30``) and an estimator ``\hat{\boldsymbol{\theta}}_2(\cdot)``
trained for moderate-to-large sample sizes (e.g., ``m > 30``), one may construct a
`PiecewiseEstimator` that dispatches ``\hat{\boldsymbol{\theta}}_1(\cdot)`` if
``m \leq 30`` and ``\hat{\boldsymbol{\theta}}_2(\cdot)`` otherwise.

See also [`trainx()`](@ref) for training estimators for a range of sample sizes.

# Examples
```
using NeuralEstimators, Flux

n = 2    # bivariate data
d = 3    # dimension of parameter vector 
w = 128  # width of each hidden layer

# Small-sample estimator
Ïˆâ‚ = Chain(Dense(n, w, relu), Dense(w, w, relu));
Ï•â‚ = Chain(Dense(w, w, relu), Dense(w, d));
Î¸Ì‚â‚ = PointEstimator(DeepSet(Ïˆâ‚, Ï•â‚))

# Large-sample estimator
Ïˆâ‚‚ = Chain(Dense(n, w, relu), Dense(w, w, relu));
Ï•â‚‚ = Chain(Dense(w, w, relu), Dense(w, d));
Î¸Ì‚â‚‚ = PointEstimator(DeepSet(Ïˆâ‚‚, Ï•â‚‚))

# Piecewise estimator with changepoint m=30
Î¸Ì‚ = PiecewiseEstimator([Î¸Ì‚â‚, Î¸Ì‚â‚‚], 30)

# Apply the (untrained) piecewise estimator to data
Z = [rand(n, m) for m âˆˆ (10, 50)]
estimate(Î¸Ì‚, Z)
```
"""
struct PiecewiseEstimator <: NeuralEstimator
	estimators
	changepoints
	function PiecewiseEstimator(estimators, changepoints)
		if isa(changepoints, Number)
			changepoints = [changepoints]
		end
		@assert all(isinteger.(changepoints)) "`changepoints` should contain integers"
		if length(changepoints) != length(estimators) - 1
			error("The length of `changepoints` should be one fewer than the number of `estimators`")
		elseif !issorted(changepoints)
			error("`changepoints` should be in ascending order")
		else
			new(estimators, changepoints)
		end
	end
end
function (estimator::PiecewiseEstimator)(Z)
	# Note that this is an inefficient implementation, analogous to the inefficient
	# DeepSet implementation. A more efficient approach would be to subset Z based
	# on changepoints, apply the estimators to each block of Z, then combine the estimates.
	changepoints = [estimator.changepoints..., Inf]
	m = numberreplicates(Z)
	Î¸Ì‚ = map(eachindex(Z)) do i
		# find which estimator to use, and then apply it
		máµ¢ = m[i]
		j = findfirst(máµ¢ .<= changepoints)
		estimator.estimators[j](Z[[i]])
	end
	return stackarrays(Î¸Ì‚)
end
Base.show(io::IO, estimator::PiecewiseEstimator) = print(io, "\nPiecewise estimator with $(length(estimator.estimators)) estimators and sample size change-points: $(estimator.changepoints)")


# ---- Helper function for initialising an estimator ----

#TODO this is not very Julian, it would be better to have constructors for each estimator type. 
#     Can do this by splitting initialise_estimator() into a DeepSet constructor that takes `d` .
#     Should have initialise_estimator() as an internal function, and instead have the public API be based on constructors of the various estimator classes. This aligns more with the basic ideas of Julia, where functions returning a certain class should be made as a constructor rather than a separate function.

"""
    initialise_estimator(p::Integer; ...)
Initialise a neural estimator for a statistical model with `p` unknown parameters.

The estimator is couched in the DeepSets framework (see [`DeepSet`](@ref)) so
that it can be applied to data sets containing an arbitrary number of
independent replicates (including the special case of a single replicate).

Note also that the user is free to initialise their neural estimator however
they see fit using arbitrary `Flux` code; see
[here](https://fluxml.ai/Flux.jl/stable/models/layers/) for `Flux`'s API reference.

Finally, the method with positional argument `data_type`is a wrapper that allows
one to specify the type of their data (either "unstructured", "gridded", or
"irregular_spatial").

# Keyword arguments
- `architecture::String`: for unstructured multivariate data, one may use a fully-connected multilayer perceptron (`"MLP"`); for data collected over a grid, a convolutional neural network (`"CNN"`); and for graphical or irregular spatial data, a graphical neural network (`"GNN"`).
- `d::Integer = 1`: for unstructured multivariate data (i.e., when `architecture = "MLP"`), the dimension of the data (e.g., `d = 3` for trivariate data); otherwise, if `architecture âˆˆ ["CNN", "GNN"]`, the argument `d` controls the number of input channels (e.g., `d = 1` for univariate spatial processes).
- `estimator_type::String = "point"`: the type of estimator; either `"point"` or `"interval"`.
- `depth = 3`: the number of hidden layers; either a single integer or an integer vector of length two specifying the depth of the inner (summary) and outer (inference) network of the DeepSets framework.
- `width = 32`: a single integer or an integer vector of length `sum(depth)` specifying the width (or number of convolutional filters/channels) in each hidden layer.
- `activation::Function = relu`: the (non-linear) activation function of each hidden layer.
- `activation_output::Function = identity`: the activation function of the output layer.
- `variance_stabiliser::Union{Nothing, Function} = nothing`: a function that will be applied directly to the input, usually to stabilise the variance.
- `kernel_size = nothing`: (applicable only to CNNs) a vector of length `depth[1]` containing integer tuples of length `D`, where `D` is the dimension of the convolution (e.g., `D = 2` for two-dimensional convolution).
- `weight_by_distance::Bool = true`: (applicable only to GNNs) flag indicating whether the estimator will weight by spatial distance; if true, a `SpatialGraphConv` layer is used in the propagation module; otherwise, a regular `GraphConv` layer is used.
- `probs = [0.025, 0.975]`: (applicable only if `estimator_type = "interval"`) probability levels defining the lower and upper endpoints of the posterior credible interval.

# Examples
```
## MLP, GNN, 1D CNN, and 2D CNN for a statistical model with two parameters:
p = 2
initialise_estimator(p, architecture = "MLP")
initialise_estimator(p, architecture = "GNN")
initialise_estimator(p, architecture = "CNN", kernel_size = [10, 5, 3])
initialise_estimator(p, architecture = "CNN", kernel_size = [(10, 10), (5, 5), (3, 3)])
```
"""
function initialise_estimator(
    p::Integer;
	architecture::String,
    d::Integer = 1,
    estimator_type::String = "point",
    depth::Union{Integer, Vector{<:Integer}} = 3,
    width::Union{Integer, Vector{<:Integer}} = 32,
	variance_stabiliser::Union{Nothing, Function} = nothing,
    activation::Function = relu,
    activation_output::Function = identity,
    kernel_size = nothing,
	weight_by_distance::Bool = true,
	probs = [0.025, 0.975]
    )

	# "`kernel_size` should be a vector of integer tuples: see the documentation for details"
    @assert p > 0
    @assert d > 0
	@assert architecture âˆˆ ["MLP", "DNN", "CNN", "GNN"]
	if architecture == "DNN" architecture = "MLP" end # deprecation coercion
    @assert estimator_type âˆˆ ["point", "interval"]
    @assert all(depth .>= 0)
    @assert length(depth) == 1 || length(depth) == 2
	if isa(depth, Integer) depth = [depth] end
	if length(depth) == 1 depth = repeat(depth, 2) end
    @assert all(width .> 0)
    @assert length(width) == 1 || length(width) == sum(depth)
	if isa(width, Integer) width = [width] end
	if length(width) == 1 width = repeat(width, sum(depth)) end
	# henceforth, depth and width are integer vectors of length 2 and sum(depth), respectively

	if architecture == "CNN"
		@assert !isnothing(kernel_size) "The argument `kernel_size` must be provided when `architecture = 'CNN'`"
		@assert length(kernel_size) == depth[1]
		kernel_size = coercetotuple.(kernel_size)
	end

	L = sum(depth) # total number of hidden layers

	# inference network
	Ï• = []
	if depth[2] >= 1
		push!(Ï•, [Dense(width[l-1] => width[l], activation) for l âˆˆ (depth[1]+1):L]...)
	end
	push!(Ï•, Dense(width[L] => p, activation_output))
	Ï• = Chain(Ï•...)

	# summary network
	if architecture == "MLP"
		Ïˆ = Chain(
			Dense(d => width[1], activation),
			[Dense(width[l-1] => width[l], activation) for l âˆˆ 2:depth[1]]...
			)
	elseif architecture == "CNN"
		Ïˆ = Chain(
			Conv(kernel_size[1], d => width[1], activation),
			[Conv(kernel_size[l], width[l-1] => width[l], activation) for l âˆˆ 2:depth[1]]...,
			Flux.flatten
			)
	elseif architecture == "GNN"
		propagation = weight_by_distance ? SpatialGraphConv : GraphConv
		Ïˆ = GNNChain(
			propagation(d => width[1], activation),
			[propagation(width[l-1] => width[l], activation) for l âˆˆ 2:depth[1]]...,
			GlobalPool(mean) # readout module
			)
	end

	if !isnothing(variance_stabiliser)
		if architecture âˆˆ ["MLP", "CNN"]
			Ïˆ = Chain(variance_stabiliser, Ïˆ...)
		elseif architecture == "GNN"
			Ïˆ = GNNChain(variance_stabiliser, Ïˆ...)
		end
	end

	Î¸Ì‚ = DeepSet(Ïˆ, Ï•)

	#TODO RatioEstimator, QuantileEstimatorDiscrete, QuantileEstimatorContinuous, PosteriorEstimator
	if estimator_type == "point"
		Î¸Ì‚ = PointEstimator(Î¸Ì‚)
	elseif estimator_type == "interval"
		Î¸Ì‚ = IntervalEstimator(Î¸Ì‚, Î¸Ì‚; probs = probs)
	end

	return Î¸Ì‚
end
coercetotuple(x) = (x...,)


# ---- Ensemble of estimators ----

#TODO Think about whether Parallel() might also be useful for ensembles (this might allow for faster computations, and immediate out-of-the-box integration with other parts of the package).

"""
	Ensemble(estimators)
	Ensemble(architecture::Function, J::Integer)
	(ensemble::Ensemble)(Z; aggr = median)

Defines an ensembleÂ based on a collection of `estimators` which,
when applied to data `Z`, returns the median
(or another summary defined by `aggr`) of the estimates.

The ensemble can be initialised with a collection of trained `estimators` and then
applied immediately to observed data. Alternatively, the ensemble can be
initialised with a collection of untrained `estimators`
(or a function defining the architecture of each estimator, and the number of estimators in the ensemble),
trained with `train()`, and then applied to observed data. In the latter case, where the ensemble is trained directly,
if `savepath` is specified both the ensemble and component estimators will be saved.

Note that `train()` currently acts sequentially on the component estimators.

The ensemble components can be accessed by indexing the ensemble; the number
of component estimators can be obtained using `length()`.

# Examples
```
using NeuralEstimators, Flux

# Data Z|Î¸ ~ N(Î¸, 1) with Î¸ ~ N(0, 1)
d = 1     # dimension of the parameter vector Î¸
n = 1     # dimension of each independent replicate of Z
m = 30    # number of independent replicates in each data set
sampler(K) = randn32(d, K)
simulator(Î¸, m) = [Î¼ .+ randn32(n, m) for Î¼ âˆˆ eachcol(Î¸)]

# Neural-network architecture of each ensemble component
function architecture()
	Ïˆ = Chain(Dense(n, 64, relu), Dense(64, 64, relu))
	Ï• = Chain(Dense(64, 64, relu), Dense(64, d))
	network = DeepSet(Ïˆ, Ï•)
	PointEstimator(network)
end

# Initialise ensemble with three component estimators 
ensemble = Ensemble(architecture, 3)
ensemble[1]      # access component estimators by indexing
ensemble[1:2]    # indexing with an iterable collection returns the corresponding ensemble 
length(ensemble) # number of component estimators

# Training
ensemble = train(ensemble, sampler, simulator, m = m, epochs = 5)

# Assessment
Î¸ = sampler(1000)
Z = simulator(Î¸, m)
assessment = assess(ensemble, Î¸, Z)
rmse(assessment)

# Apply to data
ensemble(Z)
```
"""
struct Ensemble <: NeuralEstimator
	estimators
end
Ensemble(architecture::Function, J::Integer) = Ensemble([architecture() for j in 1:J])

#TODO update savepath behaviour based on new default (with nothing)
function train(ensemble::Ensemble, args...; kwargs...)
	kwargs = (;kwargs...)
	savepath = haskey(kwargs, :savepath) ? kwargs.savepath : ""
	verbose  = haskey(kwargs, :verbose)  ? kwargs.verbose : true
	estimators = map(enumerate(ensemble.estimators)) do (i, estimator)
		verbose && @info "Training estimator $i of $(length(ensemble))"
		if savepath != "" # modify the savepath before passing it onto train
			kwargs = merge(kwargs, (savepath = joinpath(savepath, "estimator$i"),))
		end
		train(estimator, args...; kwargs...)
	end
	ensemble = Ensemble(estimators)

	if savepath != ""
		if !ispath(savepath) mkpath(savepath) end
		model_state = Flux.state(cpu(ensemble)) 
		@save joinpath(savepath, "ensemble.bson") model_state
	end

	return ensemble
end

function (ensemble::Ensemble)(Z; aggr = median)
	# Compute estimate from each estimator, yielding a vector of matrices
	# NB can be done in parallel, but I think the overhead will outweigh the benefit
	Î¸Ì‚ = [estimator(Z) for estimator in ensemble.estimators]

	# Stack matrices along a new third dimension
	Î¸Ì‚ = stackarrays(Î¸Ì‚, merge = false) # equivalent to: Î¸Ì‚ = cat(Î¸Ì‚...; dims = 3)
	
	# aggregate elementwise 
	Î¸Ì‚ = mapslices(aggr, cpu(Î¸Ì‚); dims = 3) # NB mapslices doesn't work on the GPU, so transfer to CPU 
	Î¸Ì‚ = dropdims(Î¸Ì‚; dims = 3)

	return Î¸Ì‚
end

# Overload Base functions
Base.getindex(e::Ensemble, i::Integer) = e.estimators[i]
Base.getindex(e::Ensemble, indices::AbstractVector{<:Integer}) = Ensemble(e.estimators[indices])
Base.getindex(e::Ensemble, indices::UnitRange{<:Integer}) = Ensemble(e.estimators[indices])
Base.length(e::Ensemble) = length(e.estimators)
Base.eachindex(e::Ensemble) = eachindex(e.estimators)
Base.show(io::IO, ensemble::Ensemble) = print(io, "\nEnsemble with $(length(ensemble.estimators)) component estimators")
